plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'kr.higu'
version = '1.0.6'

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    archiveClassifier = 'plain'
}

shadowJar {
    archiveClassifier = ''
    relocate 'com.google.gson', 'kr.higu.shadow.gson'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.13.2'

    testImplementation 'org.assertj:assertj-core:3.27.7'
    testImplementation 'org.mockito:mockito-core:5.8.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar

            artifact(sourcesJar) {
                classifier = 'sources'
            }

            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            groupId = group
            artifactId = 'k-oauth'
            version = version
        }
    }
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
    failOnError = false
}

test {
    useJUnitPlatform()
}